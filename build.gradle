plugins{
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'nu.studer.jooq' version '5.2.1'
}
mainClassName = 'net.toadless.monkebot.Main'
sourceCompatibility = 14

repositories {
    mavenCentral()
    maven { url 'https://m2.dv8tion.net/releases' }
    maven { url 'https://jitpack.io' }
}

dependencies {

    //Misc / Util
    implementation 'info.debatty:java-string-similarity:2.0.0'
    implementation 'io.github.classgraph:classgraph:4.8.141'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'net.jodah:expiringmap:0.5.10'

    //Web
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'io.javalin:javalin:4.3.0'

    //Eval
    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.9'

    //Discord
    implementation 'net.dv8tion:JDA:4.4.0_352'
    implementation 'com.sedmelluq:lavaplayer:1.3.78'

    //Database
    implementation 'org.postgresql:postgresql:42.3.3'
    jooqGenerator 'org.postgresql:postgresql:42.2.18'

    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.jooq:jooq:3.16.5'
}

jooq {
    version = '3.14.4'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    url = MONKE_DB_URL
                    user = MONKE_DB_USER
                    password = MONKE_DB_PASSWORD
                    driver = 'org.postgresql.Driver'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = ''
                        directory = "src/main/java/net/toadless/monkebot/objects/jooq"
                        clean = true
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}


compileJava.options.encoding = 'UTF-8'